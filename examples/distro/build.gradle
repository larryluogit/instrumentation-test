group 'io.opentelemetry.example'
version '1.0-SNAPSHOT'

buildscript {
  ext {
    otelVersion = "1.28.0"
  }
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.diffplug.spotless:spotless-plugin-gradle:6.20.0"
    classpath "gradle.plugin.com.github.johnrengelman:shadow:8.0.0"
    classpath "io.opentelemetry.instrumentation:gradle-plugins:${otelVersion}-alpha"
  }
}

subprojects {
  version = rootProject.version

  apply plugin: "java"
  apply plugin: "com.diffplug.spotless"

  ext {
    versions = [
      // this line is managed by .github/scripts/update-sdk-version.sh
      opentelemetrySdk           : otelVersion,

      // these lines are managed by .github/scripts/update-version.sh
      opentelemetryJavaagent     : otelVersion,
      opentelemetryJavaagentAlpha: "${otelVersion}-alpha",

      bytebuddy                  : "1.14.5",
      autoservice                : "1.1.1",
      junit                      : "5.10.0",
      assertj                    : "3.24.2",
    ]

    deps = [
      bytebuddy  : "net.bytebuddy:byte-buddy-dep:${versions.bytebuddy}",
      autoservice: [
        "com.google.auto.service:auto-service:${versions.autoservice}",
        "com.google.auto.service:auto-service-annotations:${versions.autoservice}",
      ]
    ]
  }

  repositories {
    mavenCentral()
  }

  spotless {
    java {
      googleJavaFormat()
      licenseHeaderFile(rootProject.file("buildscripts/spotless.license.java"), "(package|import|public)")
      target("src/**/*.java")
    }
  }

  dependencies {
    implementation(platform("io.opentelemetry:opentelemetry-bom:${versions.opentelemetrySdk}"))

    // these serve as a test of the instrumentation boms
    implementation(platform("io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom:${versions.opentelemetryJavaagent}"))
    implementation(platform("io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom-alpha:${versions.opentelemetryJavaagentAlpha}"))

    testImplementation("org.assertj:assertj-core:${versions.assertj}")

    testImplementation(enforcedPlatform("org.junit:junit-bom:${versions.junit}"))
    testImplementation("org.junit.jupiter:junit-jupiter-api:${versions.junit}")
    testImplementation("org.junit.jupiter:junit-jupiter-params:${versions.junit}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${versions.junit}")
  }

  tasks {
    test {
      useJUnitPlatform()
    }

    compileJava {
      options.release.set(8)
      options.compilerArgs << "-Werror"
    }
    compileTestJava {
      options.release.set(17)
    }
  }
}
