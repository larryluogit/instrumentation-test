ext {
  // Play doesn't work with Java 9+ until 2.6.12
  maxJavaVersionForTests = JavaVersion.VERSION_1_8
}

apply from: "$rootDir/gradle/instrumentation.gradle"
apply from: "$rootDir/gradle/test-with-scala.gradle"

muzzle {
  pass {
    group = 'com.typesafe.play'
    module = 'play_2.11'
    versions = '[2.3.0,2.4)'
    assertInverse = true
  }
  fail {
    group = 'com.typesafe.play'
    module = 'play_2.12'
    versions = '[,]'
  }
  fail {
    group = 'com.typesafe.play'
    module = 'play_2.13'
    versions = '[,]'
  }
}

dependencies {
  // TODO(anuraaga): Something about library configuration doesn't work well with scala compilation
  // here.
  // 2.3.9 is the earliest version in maven central
  compileOnly(group: 'com.typesafe.play', name: 'play_2.11', version: '2.3.9') {
    // 2.3.x versions depend on netty-http-pipelining 1.1.2 which is not in maven central
    exclude group: 'com.typesafe.netty', module: 'netty-http-pipelining'
  }

  testInstrumentation project(':instrumentation:netty:netty-3.8:javaagent')

  testLibrary(group: 'com.typesafe.play', name: 'play-java-ws_2.11', version: '2.3.9') {
    exclude group: 'com.typesafe.netty', module: 'netty-http-pipelining'
  }
  testLibrary(group: 'com.typesafe.play', name: 'play-test_2.11', version: '2.3.9') {
    exclude group: 'org.eclipse.jetty', module: 'jetty-websocket'
    exclude group: 'com.typesafe.netty', module: 'netty-http-pipelining'
  }

  latestDepTestLibrary(group: 'com.typesafe.play', name: 'play-java-ws_2.11', version: '2.3.+') {
    exclude group: 'com.typesafe.netty', module: 'netty-http-pipelining'
  }
  latestDepTestLibrary(group: 'com.typesafe.play', name: 'play-test_2.11', version: '2.3.+') {
    exclude group: 'org.eclipse.jetty', module: 'jetty-websocket'
    exclude group: 'com.typesafe.netty', module: 'netty-http-pipelining'
  }
}
