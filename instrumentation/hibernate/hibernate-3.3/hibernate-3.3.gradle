/*
 * Integration for Hibernate between 3.5 and 4.
 * Has the same logic as the Hibernate 4+ integration, but is copied rather than sharing a codebase. This is because
 * the root interface for Session/StatelessSession - SharedSessionContract - isn't present before version 4. So the
 * instrumentation isn't able to reference it.
 */

apply from: "${rootDir}/gradle/instrumentation.gradle"
apply plugin: 'org.unbroken-dome.test-sets'

muzzle {
  pass {
    group = "org.hibernate"
    module = "hibernate-core"
    versions = "[3.3.0.GA,4.0.0.Final)"
    assertInverse = true
  }
}

testSets {
  latestDepTest {
    dirName = 'test'
  }
}

dependencies {
  compileOnly group: 'org.hibernate', name: 'hibernate-core', version: '3.3.0.GA'

  compile project(':instrumentation:hibernate:hibernate-common')

  testCompile project(':instrumentation:jdbc')
  // Added to ensure cross compatibility:
  testCompile project(':instrumentation:hibernate:hibernate-4.0')
  testCompile project(':instrumentation:hibernate:hibernate-4.3')

  testCompile group: 'org.hibernate', name: 'hibernate-core', version: '3.3.0.SP1'
  testCompile group: 'org.hibernate', name: 'hibernate-annotations', version: '3.4.0.GA'
  testCompile group: 'javassist', name: 'javassist', version: '+'
  testCompile group: 'com.h2database', name: 'h2', version: '1.4.197'
  testCompile "javax.xml.bind:jaxb-api:2.2.11"
  testCompile "com.sun.xml.bind:jaxb-core:2.2.11"
  testCompile "com.sun.xml.bind:jaxb-impl:2.2.11"
  testCompile "javax.activation:activation:1.1.1"

  latestDepTestCompile group: 'org.hibernate', name: 'hibernate-core', version: '3.+'
}

configurations {
  // Needed for test, but for latestDepTest this would otherwise bundle a second incompatible version of hibernate-core.
  latestDepTestCompile.exclude group: 'org.hibernate', module: 'hibernate-annotations'
}
