apply from: "$rootDir/gradle/instrumentation.gradle"
apply plugin: 'org.unbroken-dome.test-sets'

muzzle {
  fail {
    group = "javax.ws.rs"
    module = "jsr311-api"
    versions = "[,]"
  }
  pass {
    group = "javax.ws.rs"
    module = "javax.ws.rs-api"
    versions = "[,]"
  }
}

testSets {
  latestDepTest {
    dirName = 'test'
  }

  resteasy31Test {
    dirName = 'test'
  }
}

dependencies {
  compileOnly group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0'

  testImplementation project(':instrumentation:servlet:servlet-3.0')
  testImplementation project(':instrumentation:jaxrs:jaxrs-2.0:jaxrs-2.0-jersey-2.0')
  testImplementation project(':instrumentation:jaxrs:jaxrs-2.0:jaxrs-2.0-resteasy-3.0')
  testImplementation project(':instrumentation:jaxrs:jaxrs-2.0:jaxrs-2.0-resteasy-3.1')

  // Jersey
  // First version with DropwizardTestSupport:
  testImplementation group: 'io.dropwizard', name: 'dropwizard-testing', version: '0.8.0'
  testImplementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.2.3'
  testImplementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-afterburner', version: '2.9.10'

  latestDepTestImplementation group: 'io.dropwizard', name: 'dropwizard-testing', version: '1.+'

  // Resteasy
  testImplementation group: 'org.jboss.resteasy', name: 'resteasy-jaxrs', version: '3.0.0.Final'

  resteasy31TestImplementation(group: 'org.jboss.resteasy', name: 'resteasy-jaxrs', version: '3.1.0.Final')

  latestDepTestImplementation group: 'org.jboss.resteasy', name: 'resteasy-jaxrs', version: '+'
}

integrationTest.dependsOn resteasy31Test

// Until custom source sets can use minJavaVersionForTests separately from other tests
// force this one test to run on java8 only
resteasy31Test {
  onlyIf {
    String java8Home = findJavaHome(JavaVersion.VERSION_1_8)
    java8Home != null && executable == toExecutable(java8Home)
  }
}
