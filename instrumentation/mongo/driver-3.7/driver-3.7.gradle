muzzle {
  pass {
    group = "org.mongodb"
    module = "mongo-java-driver"
    versions = "[3.7,)" // a couple of test attribute verifications don't pass until 3.8.0
    assertInverse = true
  }
  fail {
    group = "org.mongodb"
    module = "mongo-java-driver"
    versions = "(,3.7)"
    assertInverse = true
  }
}

apply from: "${rootDir}/gradle/java.gradle"

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  latestDepTest {
    dirName = 'test'
  }
}

dependencies {
  // use mongo listener
  compile(project(':instrumentation:mongo:driver-3.1')) {
    transitive = false
  }
  compileOnly group: 'org.mongodb', name: 'mongo-java-driver', version: '3.8.0'

  testCompile project(':instrumentation:mongo').sourceSets.test.output
  testCompile group: 'de.flapdoodle.embed', name: 'de.flapdoodle.embed.mongo', version: '1.50.5'

  testCompile group: 'org.mongodb', name: 'mongo-java-driver', version: '3.8.0'
  latestDepTestCompile group: 'org.mongodb', name: 'mongo-java-driver', version: '+'
}
