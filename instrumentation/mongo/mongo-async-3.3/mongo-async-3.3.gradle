// Set properties before any plugins get loaded
ext {
  // Since we're using CompletableFutures in the test...
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

apply from: "$rootDir/gradle/instrumentation.gradle"
apply plugin: 'org.unbroken-dome.test-sets'

muzzle {
  pass {
    group = "org.mongodb"
    module = "mongodb-driver-async"
    versions = "[3.3,)"
    extraDependency 'org.mongodb:mongo-java-driver:3.3.0'
    assertInverse = true
  }
}

testSets {
  latestDepTest {
    dirName = 'test'
  }
}

// this is needed to force mongo-common test classes to be built first
// so that the dependency below on its test sourceSet will work
compileTestJava.dependsOn tasks.getByPath(':instrumentation:mongo:mongo-common:testClasses')

dependencies {
  implementation(project(':instrumentation:mongo:mongo-common'))

  compileOnly group: 'org.mongodb', name: 'mongodb-driver-async', version: '3.3.0'

  testImplementation project(':instrumentation:mongo:mongo-common').sourceSets.test.output
  testImplementation group: 'de.flapdoodle.embed', name: 'de.flapdoodle.embed.mongo', version: '1.50.5'

  testImplementation group: 'org.mongodb', name: 'mongodb-driver-async', version: '3.3.0'
  latestDepTestImplementation group: 'org.mongodb', name: 'mongodb-driver-async', version: '+'

  testImplementation project(':instrumentation:mongo:mongo-3.7')
}
