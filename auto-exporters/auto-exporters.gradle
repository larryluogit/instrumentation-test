plugins {
  id "com.github.johnrengelman.shadow"
}

apply from: "${rootDir}/gradle/java.gradle"

dependencies {
  testCompile project(':auto-tooling')

  testCompile project(':auto-exporters:opentelemetry-auto-exporters-otlp')
  testCompile project(':auto-exporters:opentelemetry-auto-exporters-jaeger')
  testCompile project(':auto-exporters:opentelemetry-auto-exporters-logging')
  testCompile project(':auto-exporters:opentelemetry-auto-exporters-zipkin')
}

tasks.withType(Test).configureEach() {
  dependsOn ':auto-exporters:opentelemetry-auto-exporters-otlp:shadowJar'
  dependsOn ':auto-exporters:opentelemetry-auto-exporters-jaeger:shadowJar'
  dependsOn ':auto-exporters:opentelemetry-auto-exporters-logging:shadowJar'
  dependsOn ':auto-exporters:opentelemetry-auto-exporters-zipkin:shadowJar'
  doFirst {
    systemProperty 'projectVersion', allprojects.version[0]
    systemProperty 'adapterRoot', "${rootDir}/auto-exporters"
    systemProperty 'otlpExporterJar', project(':auto-exporters:opentelemetry-auto-exporters-otlp').tasks.shadowJar.archivePath
    systemProperty 'jaegerExporterJar', project(':auto-exporters:opentelemetry-auto-exporters-jaeger').tasks.shadowJar.archivePath
    systemProperty 'loggingExporterJar', project(':auto-exporters:opentelemetry-auto-exporters-logging').tasks.shadowJar.archivePath
    systemProperty 'zipkinExporterJar', project(':auto-exporters:opentelemetry-auto-exporters-zipkin').tasks.shadowJar.archivePath
  }
}

configurations {
  shadowInclude
}

dependencies {
  shadowInclude project(path: ':auto-exporters:opentelemetry-auto-exporters-jaeger', configuration: 'shadow')
  shadowInclude project(path: ':auto-exporters:opentelemetry-auto-exporters-logging', configuration: 'shadow')
  shadowInclude project(path: ':auto-exporters:opentelemetry-auto-exporters-otlp', configuration: 'shadow')
  shadowInclude project(path: ':auto-exporters:opentelemetry-auto-exporters-zipkin', configuration: 'shadow')
}


shadowJar {
  configurations = [project.configurations.shadowInclude]

  archiveClassifier = ''

  mergeServiceFiles()

  exclude '**/module-info.class'

  // relocate OpenTelemetry API usage
  relocate "io.opentelemetry.OpenTelemetry", "io.opentelemetry.auto.shaded.io.opentelemetry.OpenTelemetry"
  relocate "io.opentelemetry.common", "io.opentelemetry.auto.shaded.io.opentelemetry.common"
  relocate "io.opentelemetry.context", "io.opentelemetry.auto.shaded.io.opentelemetry.context"
  relocate "io.opentelemetry.correlationcontext", "io.opentelemetry.auto.shaded.io.opentelemetry.correlationcontext"
  relocate "io.opentelemetry.internal", "io.opentelemetry.auto.shaded.io.opentelemetry.internal"
  relocate "io.opentelemetry.metrics", "io.opentelemetry.auto.shaded.io.opentelemetry.metrics"
  relocate "io.opentelemetry.trace", "io.opentelemetry.auto.shaded.io.opentelemetry.trace"

  // relocate OpenTelemetry API dependency usage
  relocate "io.grpc", "io.opentelemetry.auto.shaded.io.grpc"
}
